thunkify = require 'thunkify'
ForaModel = require('./foramodel').ForaModel
ForaDbModel = require('./foramodel').ForaDbModel
utils = require('../lib/utils')
models = require('./')

class Forum extends ForaDbModel

    class Settings extends ForaModel
        @typeDefinition: {
            name: "forum-settings",
            schema: {
                type: 'object',                    
                properties: {
                    commentsEnabled: { type: 'boolean' },
                    commentsOpened: { type: 'boolean' }
                }
            }
        }

    @Settings: Settings

    class Summary extends ForaModel   
        @typeDefinition: {
            name: "forum-summary",
            schema: {
                type: 'object',        
                properties: {
                    id: { type: 'string' },
                    network: { type: 'string' },
                    name: { type: 'string' },
                    stub: { type: 'string' },
                    createdBy: { $ref: "user-summary" }
                }
                required: ['id', 'network', 'name', 'stub', 'createdBy']
            }
        }    

    @Summary: Summary

    class Stats extends ForaModel   
        @typeDefinition: {
            name: "forum-stats",
            schema: {
                type: 'object',        
                properties: {
                    posts: { type: 'number' },
                    members: { type: 'number' },
                    lastPost: { type: 'number' }
                }
                required: ['posts', 'members', 'lastPost']
            }
        }
        
    @Stats: Stats

    @childModels: { Stats, Summary, Settings }

    @typeDefinition: -> {
        name: 'forum',
        collection: 'forums',
        schema: {
            type: 'object',        
            properties: {
                network: { type: 'string' },
                name: { type: 'string' },
                stub: { type: 'string' },
                description: { type: 'string' },
                type: { type: 'string', enum: ['public', 'protected', 'private'] },
                postTypes: { type: 'array', items: 'string' },
                settings: { $ref: 'forum-settings' },
                cover: { $ref: 'cover' },
                createdBy: { $ref: 'user-summary' },
                snapshot: { type: 'object' }
                stats: { $ref: 'forum-stats' },
            }
            required: ['network', 'name', 'stub', 'description', 'type', 'postTypes', 'createdBy', 'snapshot', 'stats']
        },
        autoGenerated: {
            createdAt: { event: 'created' },
            updatedAt: { event: 'updated' }
        },        
        mapping: {
            postTypes: { format: 'csv' }
        }
        associations: {
            info: { type: "forum-info", key: { '_id': 'forumid' } }
        },
        logging: {
                onInsert: 'NEW_FORUM'
        }
    }
        
    
    save: (context, db) =>        
        if not @_id
            @stats = new Stats {
                posts: 0,
                members: 1,
                lastPost: 0
            }
            @postTypes ?= []
            @snapshot ?= { posts: [] }
        
        super


        
    summarize: =>        
        summary = new Summary {
            id: @_id.toString()
            network: @network,
            name: @name,
            stub: @stub,
            createdBy: @createdBy
        }
        
        
        
    getView: (name) =>
        switch name
            when 'card'
                {
                    id: @_id.toString()
                    @network,
                    @name,
                    @description,
                    @stub,
                    @createdBy,
                    @snapshot,
                    image: @cover?.image?.small
                }



    join: (user, token, context, db) =>*
        { context, db } = @getContext context, db
        if @type is 'public'
            yield @addRole user, 'member', context, db
        else
            throw new Error "Access denied"
        
        
        
    addPost: (post, context, db) =>*
        { context, db } = @getContext context, db
        post.forum = @summarize()
        yield post.save context, db
        


    getPosts: (limit, sort, context, db) =>*
        { context, db } = @getContext context, db
        yield models.Post.find({ 'forum.stub': @stub, 'forum.network': @network, state: 'published' }, ((cursor) -> cursor.sort(sort).limit limit), context, db)
        

    
    addRole: (user, role, context, db) =>*
        { context, db } = @getContext context, db
        
        membership = yield models.Membership.get { 'forum.id': @_id.toString(), 'user.username': user.username }, context, db
        if not membership
            membership = new (models.Membership) {
                forum: @summarize(),
                user: user,
                roles: [role]
            }
        else
            if membership.roles.indexOf(role) is -1 
                membership.roles.push role
        yield membership.save context, db   
                


    removeRole: (user, role, context, db) =>*
        { context, db } = @getContext context, db
        
        membership = yield models.Membership.get { 'forum.id': @_id.toString(), 'user.username': user.username }, context, db
        membership.roles = (r for r in membership.roles when r isnt role)
        yield if membership.roles.length then membership.save() else membership.destroy()
                
    
                                
    getMemberships: (roles, context, db) =>*
        { context, db } = @getContext context, db
        yield models.Membership.find { 'forum.id': @_id.toString(), roles: { $in: roles } }, ((cursor) -> cursor.sort({ id: -1 }).limit 200), context, db
        
      
            
    refreshSnapshot: (context, db) =>*
        { context, db } = @getContext context, db
        posts = yield @getPosts 10, { _id: -1 }, context, db
        @snapshot = { posts: (p.getView("snapshot") for p in posts) }
        if posts.length
            cursor = yield models.Post.getCursor({ 'forum.id': @_id.toString() , state: 'published' }, context, db)
            @stats.posts = yield thunkify(cursor.count).call cursor
            @stats.lastPost = posts[0].savedAt
            yield @save context, db

exports.Forum = Forum

